#Использовать v8runner
#Использовать v8storage
#Использовать json

Перем Настройки; // настройки подключения; логины и пароли

Процедура Инициализация(Отказ)

	Парсер = Новый ПарсерJSON();
	ИмяФайлаНастроек = "save_cf.ini";
	Файл = Новый Файл(ИмяФайлаНастроек);

	Если Файл.Существует() Тогда
		
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.Прочитать(ИмяФайлаНастроек, "UTF-8");
		СтрокаJSON = ТекстовыйДокумент.ПолучитьТекст();
		Настройки = Парсер.ПрочитатьJSON(СтрокаJSON,,,Истина);

	Иначе

		Сообщить(СтрШаблон("Файл настроек ""%1"" не существует. Создан файл с пустыми настройками."
			+ " Его необходимо заполнить", ИмяФайлаНастроек));
		
		Настройки = Новый Структура;
		Настройки.Вставить("ПрефиксФайлов", "<Префикс для сохраняемых файлов .cf и .cfe>");

		ПодключениеКХранилищу = Новый Структура;
		ПодключениеКХранилищу.Вставить("ПутьОсн",          "tcp://<Сервер>:<Порт>/<Путь к хранилищу осн. конф>");
		ПодключениеКХранилищу.Вставить("ПутьРасш",         "tcp://<Сервер>:<Порт>/<Путь к хранилищу расширения>");
		ПодключениеКХранилищу.Вставить("ЛогинВХранилище",  "");
		ПодключениеКХранилищу.Вставить("ПарольВХранилище", "");
		ПодключениеКХранилищу.Вставить("ИмяРасширения",    "");		
		Настройки.Вставить("ПодключениеКХранилищу", ПодключениеКХранилищу);
		
		ПодключениеКБазе = Новый Структура;
		ПодключениеКБазе.Вставить("ИмяСервера", "<Имя сервера приложений 1С>");
		ПодключениеКБазе.Вставить("ИмяБазы",    "<Имя базы на сервере приложений 1С>");
		ПодключениеКБазе.Вставить("ЛогинБазы",  "");
		ПодключениеКБазе.Вставить("ПарольБазы", "");
		Настройки.Вставить("ПодключениеКБазе", ПодключениеКБазе);
		
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.УстановитьТекст(Парсер.ЗаписатьJSON(Настройки));
		ТекстовыйДокумент.Записать(ИмяФайлаНастроек, "UTF-8");

		Отказ = Истина;

	КонецЕсли;

КонецПроцедуры // Инициализация()

Процедура СохранитьКонфигурациюХранилищВФайлы()

	ПутьОсн          = Настройки.ПодключениеКХранилищу.ПутьОсн;
	ПутьРасш         = Настройки.ПодключениеКХранилищу.ПутьРасш;
	ЛогинВХранилище  = Настройки.ПодключениеКХранилищу.ЛогинВХранилище;
	ПарольВХранилище = Настройки.ПодключениеКХранилищу.ПарольВХранилище;
	ИмяРасширения    = Настройки.ПодключениеКХранилищу.ИмяРасширения;
	ИмяСервера       = Настройки.ПодключениеКБазе.ИмяСервера;
	ИмяБазы          = Настройки.ПодключениеКБазе.ИмяБазы;
	ЛогинБазы        = Настройки.ПодключениеКБазе.ЛогинБазы;
	ПарольБазы       = Настройки.ПодключениеКБазе.ПарольБазы;
	ПрефиксФайлов    = Настройки.ПрефиксФайлов;

	ИмяФайлаБезРасширения = ПрефиксФайлов + Формат(ТекущаяДата(), "ДФ='гггг_ММ_дд'");

	// база, в которой есть расширение с нужным именем
	СтрокаСоединения = СтрШаблон("/IBConnectionString""Srvr=%1; Ref=%2""", ИмяСервера, ИмяБазы);
	Конфигуратор = Новый УправлениеКонфигуратором();	
	Конфигуратор.УстановитьКонтекст(СтрокаСоединения, ЛогинБазы, ПарольБазы);

	ХранилищеКонфигурации = Новый МенеджерХранилищаКонфигурации();
	ХранилищеКонфигурации.УстановитьУправлениеКонфигуратором(Конфигуратор);
	ХранилищеКонфигурации.УстановитьПутьКХранилищу(ПутьОсн);
	ХранилищеКонфигурации.УстановитьПараметрыАвторизации(ЛогинВХранилище, ПарольВХранилище);
	ХранилищеКонфигурации.СохранитьВерсиюКонфигурацииВФайл( , ИмяФайлаБезРасширения + ".cf");
	Сообщить("Основная конфигурация сохранена");

	ХранилищеКонфигурации.УстановитьПутьКХранилищу(ПутьРасш);
	ХранилищеКонфигурации.УстановитьПараметрыАвторизации(ЛогинВХранилище, ПарольВХранилище);
	ХранилищеКонфигурации.УстановитьРасширениеХранилища(ИмяРасширения);
	ХранилищеКонфигурации.СохранитьВерсиюКонфигурацииВФайл( , ИмяФайлаБезРасширения + ".cfe");
	Сообщить("Расширение сохранено");

КонецПроцедуры // СохранитьКонфигурациюХранилищВФайлы()

Отказ = Ложь;
Инициализация(Отказ);
Если НЕ Отказ Тогда
	СохранитьКонфигурациюХранилищВФайлы();
КонецЕсли;
Сообщить("--Конец--");